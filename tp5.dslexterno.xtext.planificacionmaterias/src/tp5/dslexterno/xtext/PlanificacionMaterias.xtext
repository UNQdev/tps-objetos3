grammar tp5.dslexterno.xtext.PlanificacionMaterias with org.eclipse.xtext.xbase.Xbase

generate planificacionMaterias "http://www.dslexterno.tp5/xtext/PlanificacionMaterias"

Model:
	(elementosPlanificacion+=Estructuras_Planificacion)* //seudo-inicializacion de variables
	(planificacion+=Planificacion)*
; 

//====================================================================================

Materias_Abiertas:	{Materias_Abiertas}
	'materias disponibles' '{' (materiasAbiertas+=Materia)* '}'
;
Aulas_Disponibles:	{Aulas_Disponibles}
	'aulas disponibles' '{' (aulasDisponibles+=Aula)* '}'
;
Nomina_Profesores:	{Nomina_Profesores}
	'profesores en nomina' '{' (profesoresLibres+=Profesor)* '}'
;
Asignacion_Materia: 
	dia=Dia rangoHorario=Rango_Horario ',' materia=[Materia] ':' cantidadAlumnos=INT 'alumnos inscriptos'      
;


//====================================================================================

Estructuras_Planificacion:
	Nomina_Profesores | Aulas_Disponibles | Materias_Abiertas 
;
Planificacion:
	'planificacion' anio=INT 'semestre' semestre=INT '{' 
		(asignacionDeMaterias+=Asignacion_Materia)*
	'}'
;

//====================================================================================

Materia:
	'materia' name=ID '{'
		'requiere:' (requerimientos+=Recurso)*
		'dictada por:' profesor=[Profesor] 'en' aula=[Aula]
	'}' 
;
Profesor:
	'profesor' name=ID 'con dedicacion' dedicacion=Dedicacion '{'
		(disponibilidad+=Disponibilidad)*
	'}'
;

Disponibilidad:
	'puede:' dia=Dia |
	'puede:' dia=Dia rangoHorario=Rango_Horario |
	'no puede' dia=Dia |
	'no puede' dia=Dia rangoHorario=Rango_Horario
;

Dedicacion:
	//los defino como clases con el nombre entre llaves, los extension methods deberian definirse con (dispatch) 
	  {Simple} 'Simple'
	| {Semi} 'Semi'
	| {Exclusiva} 'Exclusiva' 	
;
Aula:
	'aula' name=ID '{'
		'capacidad maxima:' capacidad=INT 'alumnos'
		'recursos:' (recusos+=Recurso)*
	'}'
;
Recurso:
	name=ID	
;

enum Dia:
	  Lunes
	| Martes
	| Miercoles
	| Jueves
	| Viernes
;
Horario:
	hora=INT ':' minutos=INT
;
Rango_Horario:
	'de' horaInicio=Horario 'a' horaFinal=Horario
;