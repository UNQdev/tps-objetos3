grammar tp5.dslexterno.xtext.PlanificacionMaterias with org.eclipse.xtext.xbase.Xbase

generate planificacionMaterias "http://www.dslexterno.tp5/xtext/PlanificacionMaterias"

Model:
	'profesores en nomina' '{' (profesoresLibres+=Profesor)* '}'
	'aulas' '{' (aulasDisponibles+=Aula)* '}'
	'materias disponibles' '{' (materiasAbiertas+=Materia)* '}'
	planificacion=Planificacion
; 

//====================================================================================

Planificacion:
	'planificacion' anio=INT 'semestre' semestre=INT '{' 
		'materias a dictar:' (materiasADictar+=[Materia])*
		(asignacionesDiarias+=Asignacion_Diaria)*
	'}'
;
Asignacion_Diaria:
	//desactivar la posibilidad de que se definan asignaciones de un dia que ya fue definido
	 dia=Dia '{'(asignacionesDeMaterias+=Asignacion_Materia)* '}'
;
Asignacion_Materia: 
	rangoHorario=Rango_Horario '::' materia=[Materia]
		'profesor a cargo:' profesor=[Profesor] 'aula:' aula=[Aula]
		'alumnos inscriptos:' alumnosInscriptos=INT
;

//====================================================================================

Materia:
	'materia' name=ID '{'
		'carga horaria:' cantidadHorasSemanales=INT
		'cantidad de dias a la semana:' diasSemanales=INT
		('requiere:' (requerimientos+=Recurso)+)?
	'}' 
;
Profesor:
	'profesor' name=ID 'con dedicacion' dedicacion=Dedicacion 
	'disponibilidad' '{'
		(disponibilidad+=Disponibilidad)+
	'}'
;
Disponibilidad:
	'puede:' dia=Dia (rangosHorario=Rango_Horario)? |
	'no puede' dia=Dia (rangosHorario=Rango_Horario)?
;
Dedicacion: 
	  {Simple} 'Simple'
	| {Semi} 'Semi'
	| {Exclusiva} 'Exclusiva' 	
;
Aula:
	'aula' name=ID '{'
		'capacidad maxima:' capacidad=INT 'alumnos'
		('recursos:' (recusos+=Recurso)+)?
	'}'
;
Recurso:
	name=ID	
;

enum Dia:
	Lunes | Martes | Miercoles | Jueves | Viernes | Sabado
;
Horario:
	hora=INT (':' minutos=INT)?
;
Rango_Horario:
	'de' horaInicio=Horario 'a' horaFinal=Horario
;